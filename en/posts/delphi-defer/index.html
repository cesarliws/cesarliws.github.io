<!DOCTYPE html>
<html lang="en-US">
  <head>
    <meta charset="utf-8">
    <meta http-equiv="X-UA-Compatible" content="IE=edge,chrome=1">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <meta name="author" content="Cesar Romero">
    <meta name="description" content="">
    <meta name="generator" content="Hugo 0.20.2" />

    
    <title>Delphi Defer &middot; @cesarliws’s cooking bytes</title>

    <link rel="stylesheet" href="https://cesarliws.github.io/css/weblog.css" media="screen" charset="utf-8" />
    <link rel="stylesheet" href="https://cdnjs.cloudflare.com/ajax/libs/lightgallery/1.2.14/css/lightgallery.min.css" type="text/css" />
    

    
	<link href="https://cesarliws.github.io/index.xml" rel="alternate" type="application/rss+xml" title="cooking bytes" />
	
  </head>
  <body class="site">

<header>
  <nav>
    <h1><a href="/">@cesarliws</a>’s cooking bytes.</h1>
    <div class="menu">
      
      <ul>
        
            <li>
              <a href="/">Home</a>
              
                <a href="/index.xml"><img src="/img/rss.svg" class="rss-icon" /></a>
              
            </li>
        
            <li>
              <a href="/posts/">Posts</a>
              
                <a href="/posts/index.xml"><img src="/img/rss.svg" class="rss-icon" /></a>
              
            </li>
        
            <li>
              <a href="/en/">English</a>
              
                <a href="/en/index.xml"><img src="/img/rss.svg" class="rss-icon" /></a>
              
            </li>
        
        
      </ul>
    </div>
  </nav>
</header>


  <main class="content">

    <article class="post">
      
        <h2><a rel="full-article" href="https://cesarliws.github.io/en/posts/delphi-defer/">Delphi Defer</a></h2>
      

      <div class="postmeta">Posted on <time datetime="2017-04-22 14:41:40 -0300 BRT" pubdate="">April 22, 2017</time> in
        <span class="categories">
          
        </span>
      </div>

      <p>
        

<p><a href="/posts/delphi-defer/">Versão deste artigo em Português</a></p>

<p>Few weeks ago I was refactoring my framework &ldquo;Foundation 4 Delphi&rdquo;, I had to extend the TComponent.TRecursiveEnumerator to be used in other classes, it was a very old implementation and I decided to rewrite with generics and take advantage of smart records to simplify the integration with my TComponentHelper class helper.</p>

<p>In the record’s implementation, I had to create a “IInterface” field just to be able to simulate a destructor in the record, when I thought how great would be if we had the <a href="https://golang.org/doc/effective_go.html#defer" target="_blank">Defer</a> function as in Golang. When I was writing the unit tests, I felt again the need for Defer, so I decided to think more about the pattern and behavior of Defer, which led me to try to implement it in Delphi just as a logic exercise, even thinking that it would not be something useful for production, but in the end of the day, I got something very useful.</p>

<h2 id="what-is-defer">What is Defer?</h2>

<p><img src="/img/postponed.jpg" alt="Postpone image" /> Defer defines the “postpone procedure” pattern, this postpone should schedule a “procedure: TProc” to run it after the end of the caller method that executed the call to the Defer function (Proc: TProc).</p>

<p>At the end of the caller method, Defer must execute all stacked procedures in the reverse order in which they were scheduled.</p>

<p><a href="https://golang.org/doc/effective_go.html#defer" target="_blank">Defer according to the Golang documentation </a>:</p>

<blockquote>
<p>Go&rsquo;s defer statement schedules a function call (the deferred function) to be run immediately before the function executing the defer returns. It&rsquo;s an unusual but effective way to deal with situations such as resources that must be released regardless of which path a function takes to return. The canonical examples are unlocking a mutex or closing a file.</p>
</blockquote>

<p>It is important to know that Golang is a language with garbage collection, so it is not necessary to destroy the created objects, only to deallocate resources.</p>

<p>And regardless of the executed flow in the function, Defer should always execute the already scheduled methods, so if there is an Exit before the end or if an exception occurs, all the scheduled methods must be executed.</p>

<p>In my research, I discovered that Defer was implemented for Apple&rsquo;s Swift, version 2.0. You can read more about that in this article with a nice title &ldquo;try / finally done right&rdquo;:</p>

<ul>
<li><a href="https://www.hackingwithswift.com/new-syntax-swift-2-defer" target="_blank">The defer keyword in Swift 2: try/finally done right</a></li>
</ul>

<p>I also read on reddit that this feature exists in D language, but I haven&rsquo;t found any references.</p>

<h3 id="some-uses-for-defer">Some uses for Defer</h3>

<ul>
<li>Closing database connection, transaction, or query</li>
<li>Closing socket connection</li>
<li>Closing opened files</li>
<li>Unlocking critical section/mutex</li>
</ul>

<h2 id="the-implementation">The Implementation</h2>

<p>After understanding how a Defer implementation should work, I implemented it using Interfaces, in this way the object would be automatically deallocated in the &ldquo;end&rdquo; statement of the caller method.</p>

<pre>
  IDeferrer = Interface(IInterface)
    function Defer(Proc: TProc): IDeferrer;
  end;
</pre>

<p>I followed the Golang standard for naming interfaces, adding the suffix &ldquo;er&rdquo;. This pattern differs from what we usually see in Delphi, add the suffix &ldquo;able&rdquo;, like in &ldquo;Deferrable&rdquo;.</p>

<p>Internally, it has a stack with the scheduled TProc list, these methods will be executed when the TDefer object is destroyed, in the reverse order in which they were scheduled. Delphi already have a TStack&lt;T&gt; implementation in System.Generics.Collections, I just had to decorate TProc with a TStackItem record before storing.</p>

<pre>
type
  TDeferrer = class(TInterfacedObject, IDeferrer)
  strict private
    type
      TStackItem = record
        Proc: TProc;
      end;
  strict private
    FStack: TStack< TStackItem >;
    procedure Push(Proc: TProc);
    procedure ProcessStack;
  public
    constructor Create; overload;
    constructor Create(Proc: TProc); overload;
    destructor Destroy; override;
    function Defer(Proc: TProc): IDeferrer;
  end;
</pre>

<p>To finish, I created a global function that returns the created instance already storing the first method:</p>

<pre>
function Defer(Proc: TProc): IDeferrer;
begin
  Result := TDeferrer.Create(Proc);
end;
</pre>

<h2 id="how-it-works">How it works</h2>

<p>The purpose of Defer is to defer methods execution, it not to manage the lifetime of objects, but as in Delphi Win32/Win64 objects are manually managed, we can use Defer to destroy objects, as well to finalize other resources.</p>

<pre>
uses
  Foundation.System;
var
  Database: TDatabase;
  Exec: IDeferrer;
  Query: TQuery;
  Transaction: TTransaction;
begin
  Database := TDatabase.Create(FWriter);
  Exec := Defer(Database.Free);

  Database.Open('foundation-db');
  Exec.Defer(Database.Close);
  
  Transaction := Database.StartTransaction;
  Exec.Defer(Transaction.Free);
  Exec.Defer(Transaction.Commit);
  
  Query := Transaction.Query;
  Exec.Defer(Query.Free);
  
  if Query.Open('select value from table') then
  begin
    Exec.Defer(Query.Close);
  end;
end;
</pre> 

<p>This code is part of the unit tests. The objects are just Mock, the FWriter object passed in the constructor is used to write the steps so the results can be validated, this was the way I found to create tests for Defer, as it will only run when being destroyed.</p>

<p>This is the case test log used to check the results:</p>

<pre>
  TDatabase.Open('foundation-db')
  TDatabase.StartTransaction
  TTransaction.Query
  TQuery.Create
  TQuery.Open('select value from table')

> "Defer execution starts here"

  TQuery.Close 
  TQuery.Free
  TTransaction.Commit
  TTransaction.Free
  TDatabase.Close
  TDatabase.Free
</pre> 

<p>Note that Defer happens only after TQuery.Open, exactly in the reverse order in which the methods were scheduled in Defer.</p>

<p>Another detail is the declaration of the &ldquo;Exec: IDeferrer&rdquo; variable to have only one instance. Declaring the variable is not required, and the code will be cleaner with just a small overhead.</p>

<p>If the IDeferrer reference is not stored in a local variable, a new instance is created for each Defer call. That would be a problem if the order in which the instances are deallocated was not the same as Defer, but the behavior was consistent across all tests. I assume it is the default behavior of <a href="https://github.com/pleriche/FastMM4" target="_blank">FastMM</a>, the Delphi default memory manager, to store the allocated instances in a stack, so the order to deallocate is the same as we need for Defer&rsquo;s implementation. If your project memory manager is another one like <a href="https://github.com/andremussche/scalemm/tree/version2" target="_blank">ScaleMM</a> or <a href="https://www.nexusdb.com/support/index.php?q=node/522" target="_blank">Nexus Memory Manager</a> I suggest doing tests before using Defer, although I assume this should be the default behaviour for deallocating resources.</p>

<h3 id="an-alternative-implementation">An alternative implementation</h3>

<p>In the Experimental folder of the repository there is an alternate implementation that extends the Defer in the <a href="https://github.com/cesarliws/foundation-4-delphi/blob/master/Experimental/Foundation.Pattern.Defer.Auto.pas" target="_blank">Foundation.Pattern.Defer.Auto</a> unit.</p>

<p>This implementation captures the thread id and caller method pointer to be used as the key to save the Defer instance in a TDictionary&lt;string, IDeferrer&gt; to always reuse the same instance. I have successfully tested this implementation on Win32, Win64 and in single and multi thread.</p>

<p>To capture the caller method reference, the <a href="https://github.com/project-jedi/jcl/blob/master/jcl/source/windows/JclDebug.pas" target="_blank">JclDebug</a> unit from the Jedi Jcl project is used. For win32 I managed to extract only the necessary lines of code to the <a href="https://github.com/cesarliws/foundation-4-delphi/blob/master/Experimental/Foundation.Vendor.JclDebug.pas" target="_blank">Foundation.Vendor.JclDebug</a> unit, just to make easy to test, along with the author’s credits and license. For production and Win64, I recommend you to use the latest JclDebug unit.</p>

<p>The advantage is clear, only one instance of Defer is created for each caller method, on the other hand the overhead generated from the implementation of the list using the TDictionary class, the mutex needed to ensure good multi-thread behavior and Jcl dependency, they are bigger than just declaring a variable or having more than one instance of the Defer method caller. Assuming that in common applications, there will not be many instances at the same time.</p>

<h3 id="more-examples">More examples</h3>

<h4 id="anonymous-methods">Anonymous methods</h4>

<pre>
  Database := TDatabase.Create(FWriter);
  Database.Open('database-name');
  Defer(
    procedure
    begin
      Database.Close;
      Database.Free;
    end
  );
</pre> 

<h4 id="method-tracer">Method tracer</h4>

<pre>
class function TTrace.Method(Writer: TStringsWriter; const MethodName: string; var TraceProc: ITracer): IDeferrer;
var
  {! Workaround "Defer(ITracer.Exit)" : E2010 Incompatible types: 'TProc' and 'procedure, untyped pointer or untyped parameter' }
  Trace: TTrace;
begin
  Trace := TTrace.Create(Writer);
  Trace.Enter(MethodName);
  Result := Defer(Trace.Exit);
  Supports(Trace, ITracer, TraceProc)
end;

var
  Trace: ITracer;
begin
  TTrace.Method(FWriter, 'DelegateDeferToTraceExecute', Trace);
  Trace.Step('First');
  Trace.Step('Second');
  Trace.Step('Third');
end;
</pre> 

<p>The tracer&rsquo;s output log:</p>

<pre>
    > Enter DelegateDeferToTraceExecute
        1. DelegateDeferToTraceExecute: First
        2. DelegateDeferToTraceExecute: Second
        3. DelegateDeferToTraceExecute: Third
    < Exit DelegateDeferToTraceExecute
</pre> 

<h4 id="conditional-finalization-in-anonymous-method">Conditional finalization in anonymous method</h4>

<pre>
Defer(
  procedure(
    if Datatabase.InTransaction then
    begin
      if Object.IsValid then
          Datatabase.Commit
      else
          Datatabase.RollBack;
    end;
  )
);
</pre> 

<h4 id="handling-exceptions">Handling exceptions</h4>

<pre>
Defer(
  procedure(
    try
      Connection.Close;
    except 
      on E: Exception do
        Log.Write(E); 
    end;

    Connection.Free;
  )
);
</pre> 
  

<h4 id="defer-as-a-record-destructor">Defer as a record destructor</h4>

<pre>
type
  TValueType< T > = record
  private
    FAnyObject: TAnyObject;
    FDeferrer: IDeferrer;
    FValue: T;
  public
    constructor Create(Value: T);
  end;  

constructor TValueType< T >.Create(Value: T);
begin
  FValue := Value;
  FAnyObject := TAnyObject.Create
  FDeferrer  := Defer(FAnyObject.Free);
end;
</pre> 

<h2 id="advantages">Advantages</h2>

<p>Delaying the execution of procedures has some advantages:</p>

<ul>
<li><p>Ensures that you will not forget to finalize a resource, a very common error while doing code maintenance and new conditions are added creating a new flow.</p></li>

<li><p>The finalization code is next to the initialization code, which is more readable than putting at the end of the method;</p></li>

<li><p>No need for try/finally blocks to ensure that a resource is finalized.</p></li>

<li><p>Even if an exception occurs, all scheduled methods will be executed.</p></li>
</ul>

<h2 id="caveats">Caveats</h2>

<ul>
<li><p>Defer adds a small overhead by the created &ldquo;Defer&rdquo; object, the finalization will accumulate the execution of procedures, code that should be executed anyway. I consider that not being a high performance code, the use of defer brings more advantages than disadvantages.</p></li>

<li><p>Avoid using defer inside loops, if it is necessary to allocate and deallocate resources within a loop, the deallocation must be deterministic. Excessive usage can greatly increase memory consumption and cause slowness.</p></li>

<li><p>When using with anonymous methods keep in mind that the state is captured, any variable used will have the value captured at the time the defer is declared and not the value changed during the procedure flow. Read more in <a href="https://community.embarcadero.com/blogs/entry/understanding-anonymous-methods-37828" target="_blank">Understanding Anonymous Methods</a></p></li>

<li><p>This project was inspired by Golang <a href="https://golang.org/doc/effective_go.html#defer" target="_blank">Defer</a> function and has no relation to  &ldquo;Deferred/Promise&rdquo; and &ldquo;Deferred Choice&rdquo; patterns.</p></li>
</ul>

<h2 id="repository">Repository</h2>

<ul>
<li><a href="https://github.com/cesarliws/foundation-4-delphi" target="_blank">Foundation 4 Delphi</a></li>
</ul>

<p>The project is hosted on github, I will make another post just to talk about the framework and what I am preparing for it. A lot is ready and in production, you can expect news soon.</p>

      </p>
      <comments>
        <div id="disqus_thread"></div>
<script type="text/javascript">
(function() {
    if (window.location.hostname == "localhost")
        return;
    var dsq = document.createElement('script'); dsq.type = 'text/javascript'; dsq.async = true;
    var disqus_shortname = 'cesarliws';
    dsq.src = '//' + disqus_shortname + '.disqus.com/embed.js';
    (document.getElementsByTagName('head')[0] || document.getElementsByTagName('body')[0]).appendChild(dsq);
    var disqus_config = function () { 
        this.language = "en";
    };
})();
</script>
<noscript>
    Please enable JavaScript to view the 
    <a href="http://disqus.com/?ref_noscript">comments powered by Disqus.</a>
</noscript>
<a href="http://disqus.com/" class="dsq-brlink">comments powered by <span class="logo-disqus">Disqus</span></a>
      </comments>
    </article>
  </main>

<footer>
  <div class="footer_text">
      <div>
        <h3>Hi, <a href="">I'm Cesar</a></h3>
        <p>
          Software Architect at <a href="www.triersistemas.com.br">Trier Sistemas</a>, and
<br /> <a href="https://www.embarcadero.com/embarcadero-mvp-program">Embarcadero MVP</a>.
<br /> Regular speaker at software conferences. 
<br /> [desktop, cloud and mobile in mostly in <a href="https://www.embarcadero.com/products/delphi">Delphi</a> and <a href="https://golang.org/">Golang</a>]

        </p>
      </div>
  </div>

  
    <div class="footer_copyright">
      &copy; 2017 <a href="">Cesar Romero</a>
       - Made in Tubarão - SC, Brazil
    </div>
  
</footer>

    <script type="text/javascript" src="https://cdn.jsdelivr.net/jquery/2.2.0/jquery.min.js"></script>
    <script type="text/javascript" src="https://cdnjs.cloudflare.com/ajax/libs/lightgallery/1.2.14/js/lightgallery-all.min.js" charset="utf-8"></script>
    <script src="/js/weblog.js" charset="utf-8"></script>
    
    
    
<script>
window.ga=window.ga||function(){(ga.q=ga.q||[]).push(arguments)};ga.l=+new Date;
ga('create', 'UA-96993514-1', 'auto');
ga('send', 'pageview');
</script>
<script async src='//www.google-analytics.com/analytics.js'></script>

  </body>
</html>

